CLASS zcl_ddevx_aoc_0202 DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_oo_adt_classrun .
  PROTECTED SECTION.
  PRIVATE SECTION.
    TYPES: BEGIN OF type_password,
             rule_min  TYPE i,
             rule_max  TYPE i,
             rule_char TYPE string,
             password  TYPE string,
           END OF type_password.
    TYPES: type_password_table TYPE STANDARD TABLE OF type_password.

    DATA passwords TYPE type_password_table.

    "types table_type_pass TYPE STANDARD TABLE OF type_password.
    METHODS get_number_of_correct_passwds
      IMPORTING
        i_string_table                     TYPE z_ddevx_aoc_string_table
      RETURNING
        VALUE(r_number_of_correct_passwds) TYPE i.
    METHODS password_is_correct
      IMPORTING
        i_password_line TYPE zcl_ddevx_aoc_0202=>type_password
      RETURNING
        VALUE(r_result) TYPE abap_bool.
    METHODS get_table
      IMPORTING
        i_string_table TYPE z_ddevx_aoc_string_table.


ENDCLASS.



CLASS zcl_ddevx_aoc_0202 IMPLEMENTATION.


  METHOD if_oo_adt_classrun~main.
    DATA i TYPE i.
    DATA(string_table) = zcl_ddevx_input=>create('https://raw.githubusercontent.com/ddevx/aoc2020/main/input_02_01')->get_lines_as_strings( ).

    out->write( get_number_of_correct_passwds( string_table ) ).

  ENDMETHOD.

  METHOD get_number_of_correct_passwds.
    get_table( i_string_table ).

    r_number_of_correct_passwds = 0.

    LOOP AT me->passwords INTO DATA(password_line).
      IF password_is_correct( password_line ).
        r_number_of_correct_passwds = r_number_of_correct_passwds + 1.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.




  METHOD get_table.
    DATA: password_line LIKE LINE OF me->passwords.
    CLEAR me->passwords.
    LOOP AT i_string_table INTO DATA(string_line).
      CLEAR password_line.
      password_line-rule_min = substring_before( val = string_line sub = '-' ).
      password_line-rule_max = substring_before( val = substring_after( val = string_line sub = '-' ) sub = | | ).
      password_line-rule_char = substring_before( val = substring_after( val = string_line sub = | | ) sub = ':' ).
      password_line-password = substring_after( val = string_line sub = |: | ).
      APPEND password_line TO me->passwords.
    ENDLOOP.
  ENDMETHOD.


  METHOD password_is_correct.
    DATA(char1) = substring( val = i_password_line-password off = ( i_password_line-rule_min - 1 ) len = 1 ).
    DATA(char2) = substring( val = i_password_line-password off = ( i_password_line-rule_max - 1 ) len = 1 ).

    IF    ( char1 = i_password_line-rule_char AND char2 <> i_password_line-rule_char )
       OR ( char1 <> i_password_line-rule_char AND char2 = i_password_line-rule_char ).
      r_result = abap_true.
    ELSE.
      r_result = abap_false.
    ENDIF.
  ENDMETHOD.

ENDCLASS.
